


Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;	
	Поля.Добавить("Наименование");
	Поля.Добавить("ВерсияКонфигурации");

КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Представление = Данные.Наименование + " (" + Данные.ВерсияКонфигурации + ")";	

КонецПроцедуры

Функция ПолучитьСписокНовыхКонфигураций() Экспорт
	
	КаталогШаблоновКонфигураций = Константы.КаталогШаблоновКонфигураций.Получить();
	
	Если КаталогШаблоновКонфигураций.Пустая() Тогда
		//TODO: выводить в форму предупреждения с гиперссылкой на настройки (если доступны)
		ОбщегоНазначения.СообщитьПользователю("Не указан каталог шаблонов конфигурации!");
	КонецЕсли;
	
	МассивСекцийШаблонов = НайтиФайлы(КаталогШаблоновКонфигураций, "*.mft", Истина);
	
	ТаблицаФайлов = Новый ТаблицаЗначений();
	ТаблицаФайлов.Колонки.Добавить("ПутьККаталогу", ОбщегоНазначения.ОписаниеТипаСтрока(512));
	ТаблицаФайлов.Колонки.Добавить("ПутьКМанифесту", ОбщегоНазначения.ОписаниеТипаСтрока(512));
	
	Для Каждого НайденыйФайл Из МассивСекцийШаблонов Цикл
		НоваяСтрокаТаблицыФайлов = ТаблицаФайлов.Добавить(); 
		НоваяСтрокаТаблицыФайлов.ПутьККаталогу		= НайденыйФайл.Путь;
		НоваяСтрокаТаблицыФайлов.ПутьКМанифесту	= НайденыйФайл.ПолноеИмя;	
	КонецЦикла;
	
	СписокНовыхКонфигураций = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
					|	ТаблицаПрочитанныйФайлов.ПутьККаталогу,
					|	ТаблицаПрочитанныйФайлов.ПутьКМанифесту
					|ПОМЕСТИТЬ ВТ_ПутиККаталогам
					|ИЗ
					|	&ТаблицаПрочитанныйФайлов КАК ТаблицаПрочитанныйФайлов
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ
					|	ВТ_ПутиККаталогам.ПутьККаталогу,
					|	ВТ_ПутиККаталогам.ПутьКМанифесту
					|ИЗ
					|	ВТ_ПутиККаталогам КАК ВТ_ПутиККаталогам
					|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Конфигурации КАК Конфигурации
					|		ПО ВТ_ПутиККаталогам.ПутьККаталогу = Конфигурации.ПутьККаталогу
					|ГДЕ
					|	Конфигурации.Ссылка ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("ТаблицаПрочитанныйФайлов", ТаблицаФайлов);
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат СписокНовыхКонфигураций;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтруктураОписанияКонфигурации = ПолучитьСтруктуруМанифестаКонфигурации(Выборка.ПутьКМанифесту);
		Представление = 	СтруктураОписанияКонфигурации.Синоним + " (" + СтруктураОписанияКонфигурации.ВерсияКонфигурации + ")";
		СписокНовыхКонфигураций.Добавить(СтруктураОписанияКонфигурации, Представление);
	
	КонецЦикла;

	Возврат СписокНовыхКонфигураций;
	
КонецФункции

// Получить структуру манифеста конфигурации.
// 
// Параметры:
//  ПутьКФайлу - Строка -Путь к файлу
// 
// Возвращаемое значение:
//  Структура - Получить структуру манифеста конфигурации:
// * Синоним - Строка - Представление
// * ИмяКонфигурации - Строка - 
// * ВерсияКонфигурации - Строка
// * Вендор - Строка
// * ВерсияПлатформы - Строка
// * МассивСекций - Массив - Структур
Функция ПолучитьСтруктуруМанифестаКонфигурации(ПутьКФайлу)

	СтруктураОписанияКонфигурации = Новый Структура;
	СтруктураОписанияКонфигурации.Вставить("Синоним");
	СтруктураОписанияКонфигурации.Вставить("ИмяКонфигурации");
	СтруктураОписанияКонфигурации.Вставить("ВерсияКонфигурации");
	СтруктураОписанияКонфигурации.Вставить("Вендор");
	СтруктураОписанияКонфигурации.Вставить("ВерсияПлатформы");
	СтруктураОписанияКонфигурации.Вставить("ПутьККаталогу");
	СтруктураОписанияКонфигурации.Вставить("МассивСекций");
	
	ЧтениеТекстаФайла = Новый ЧтениеТекста(ПутьКФайлу);
	СтрокаФайла = ЧтениеТекстаФайла.ПрочитатьСтроку();
	
	СтруктураПути = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ПутьКФайлу);
	СтруктураОписанияКонфигурации.ПутьККаталогу = СтруктураПути.Путь;
	
	СтруктураСекции = Неопределено;
	МассивСекций = Новый Массив;
	Пока СтрокаФайла <> Неопределено Цикл
	
	    ДанныеСтроки = СтрРазделить(СтрокаФайла, "=");
	    
	    Параметр = ДанныеСтроки[0];
	    Если ДанныеСтроки.Количество() > 1 Тогда
	    	Значение = СокрЛП(ДанныеСтроки[1]);
	    Иначе
			Значение = "";
	    КонецЕсли;
	    
	    Если Параметр = "Vendor" Тогда
	    	СтруктураОписанияКонфигурации.Вендор = Значение;	
	    ИначеЕсли Параметр = "Name" Тогда	
	    	СтруктураОписанияКонфигурации.ИмяКонфигурации = Значение;
	    ИначеЕсли Параметр = "Version" Тогда	
	    	СтруктураОписанияКонфигурации.ВерсияКонфигурации = Значение;
	    ИначеЕсли Параметр = "AppVersion" Тогда		    	
	    	СтруктураОписанияКонфигурации.ВерсияПлатформы = Значение;
	    ИначеЕсли Лев(Параметр, 1) = "[" Тогда
		    	
			Если СтруктураСекции <> Неопределено Тогда
				МассивСекций.Добавить(СтруктураСекции);
			КонецЕсли;
			
			Секция = Сред(Параметр, 1, СтрДлина(Параметр) - 1);
			
			СтруктураСекции = Новый Структура;
			СтруктураСекции.Вставить("Секция", Секция);
			СтруктураСекции.Вставить("ПутьКИсточнику", "");
			СтруктураСекции.Вставить("ИмяКаталога", "");	
			СтруктураСекции.Вставить("ИмяФайла", "");
			СтруктураСекции.Вставить("ТипФайла", "");	
	    
	    ИначеЕсли Лев(Параметр, 7) = "Catalog" Тогда
	    	СоставКаталога = СтрРазделить(Значение, "/");
	    	СтруктураСекции.	ИмяКаталога = СоставКаталога[СоставКаталога.ВГраница()];	
	    	СтруктураОписанияКонфигурации.Синоним = СоставКаталога[0];
	    ИначеЕсли Параметр = "Destination" Тогда
	    	СтруктураСекции.	ПутьКИсточнику = Значение;
	    ИначеЕсли Параметр = "Source" Тогда
	    	СтруктураСекции.	ИмяФайла = Значение;
	    	ПозицияРазделителя = СтрНайти(Значение, ".");
	    	Расширение = Прав(Значение, СтрДлина(Значение) - ПозицияРазделителя);
	    	Попытка
	    		СтруктураСекции.ТипФайла = Перечисления.ТипыФайловПоставкиКонфигурации[Расширение];	
	    	Исключение
	    		ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
	    	КонецПопытки;
	    КонецЕсли;
	    
	    СтрокаФайла = ЧтениеТекстаФайла.ПрочитатьСтроку();
	КонецЦикла;
	
	Если Секция <> Неопределено Тогда
		МассивСекций.Добавить(СтруктураСекции);
	КонецЕсли;
	
	СтруктураОписанияКонфигурации.МассивСекций = МассивСекций;
	
	Возврат СтруктураОписанияКонфигурации;
	
КонецФункции


Процедура ДобавитьКонфигурациюПоОписанию(СтруктураРеквизитов, Конфигурация = Неопределено, Отказ = Ложь) Экспорт
	
	Если Конфигурация = Неопределено Тогда
		НовыйЭлемент = СоздатьЭлемент();
	Иначе
		НовыйЭлемент = Конфигурация.ПолучитьОбъект();
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(НовыйЭлемент, СтруктураРеквизитов);
	НовыйЭлемент.Наименование = СтруктураРеквизитов.Синоним;
	НовыйЭлемент.ФайлыПоставки.Очистить();
	
	Для Каждого МассивСекций Из СтруктураРеквизитов.МассивСекций Цикл
		НоваяСтрокаФайлов = НовыйЭлемент.ФайлыПоставки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаФайлов, МассивСекций);
	КонецЦикла;
	
	Попытка
		НовыйЭлемент.Записать();	
	Исключение
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры
