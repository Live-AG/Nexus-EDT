
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ТипЗнч(ПараметрыВыполненияКоманды.Источник) = Тип("ФормаКлиентскогоПриложения")
		И ПараметрыВыполненияКоманды.Источник.ИмяФормы = "Обработка.МониторИнформационныхБаз.Форма.ФормаМонитора" Тогда
		ВыбранныеБазы = ПараметрыВыполненияКоманды.Источник.Элементы.ТаблицаИнформационныхБаз.ВыделенныеСтроки;
		ИмяИБ = "Список баз";
	Иначе
		ВыбранныеБазы = Новый Массив;			
		ВыбранныеБазы.Добавить(ПараметрКоманды);
		ИмяИБ = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(Строка(ПараметрКоманды));		
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.Расширение = "v8i";
	Диалог.Фильтр = "(*.v8i)|*.v8i";
	Диалог.ПолноеИмяФайла = ИмяИБ + ".v8i";
	
	ОписаниеОповещения = Новый ОписаниеОповещения("СохранитьЯрлык", ЭтотОбъект, ВыбранныеБазы);
	ФайловаяСистемаКлиент.ПоказатьДиалогВыбора(ОписаниеОповещения, Диалог);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьЯрлык(ВыбранныеФайлы, ВыбранныеБазы) Экспорт
	
	Если ВыбранныеФайлы = Неопределено Или ВыбранныеФайлы.Количество() = 0 Тогда
		Возврат;		
	КонецЕсли;
	
	ПутьКФайлу = ВыбранныеФайлы[0];
	ТекстФайла = ПолучитьТекстФайлаЯрлыка(ВыбранныеБазы);
	
#Если МобильноеПриложениеКлиент Тогда
	
	ФайлЯрлыка = Новый ЗаписьТекста(ПутьКФайлу);
	
#Иначе
		
	ФайлЯрлыка = Новый ЗаписьТекста(ПутьКФайлу, КодировкаТекста.UTF8);
		
#КонецЕсли
 
	ФайлЯрлыка.Записать(ТекстФайла);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТекстФайлаЯрлыка(ИнформационныеБазы, ПозицияБазыВСписке = Неопределено, ИмяГруппыБаз = "")
	
	ШаблонТекстаЯрлыка = "[%Наименование%]
							|Connect=Srvr=""%Кластер%"";Ref=""%ИмяБазы%"";
							|ID=%ИдентификаторБазы%
							|OrderInList=%КодВЛисте%
							|Folder=/%ИмяГруппыБаз%
							|OrderInTree=%КодВДереве%
							|External=0
							|ClientConnectionSpeed=Normal
							|App=%ОсновнойРежимЗапуска%
							|WA=1
							|Version=%Версия%
							|DefaultVersion=%ВерсияПлатформы%
							|AppArch=%Разрядность%
							|";
	
	ВерсияПлатформы = "8.3";
	
	Если ПозицияБазыВСписке = Неопределено Тогда
		ПозицияБазыВСписке = ТекущаяУниверсальнаяДатаВМиллисекундах();	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
						|	ИнформационныеБазы.Наименование КАК Наименование,
						|	ИнформационныеБазы.Владелец.Наименование КАК Сервер1СНаименование,
						|	ИнформационныеБазы.Владелец.ПортКластера КАК Сервер1СПортКластера,
						|	ИнформационныеБазы.ИмяИБ КАК ИмяИБ,
						|	ИнформационныеБазы.ПараметрыЗапуска КАК ПараметрыЗапуска,
						|	ИнформационныеБазы.ИдентификаторБазы
						|ИЗ
						|	Справочник.ИнформационныеБазы КАК ИнформационныеБазы
						|ГДЕ
						|	ИнформационныеБазы.Ссылка В (&ИнформационныеБазы)
						|
						|УПОРЯДОЧИТЬ ПО
						|	Наименование";
	
	Запрос.УстановитьПараметр("ИнформационныеБазы", ИнформационныеБазы);
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	МассивТекстовЯрлыков = Новый Массив;
	СчетчикЭлементов = 1;
	Пока Выборка.Следующий() Цикл

		ПараметрыЗапуска	 = Выборка.ПараметрыЗапуска.Получить();
		Если Не ЗначениеЗаполнено(ПараметрыЗапуска) Тогда
			ПараметрыЗапуска = Новый Структура;	
			ПараметрыЗапуска.Вставить("ВариантАутентификации",			0);
			ПараметрыЗапуска.Вставить("Версия",								"8.3");
			ПараметрыЗапуска.Вставить("ДополнительныеПараметрыЗапуска","");
			ПараметрыЗапуска.Вставить("ОсновнойРежимЗапуска",			"Auto");
			ПараметрыЗапуска.Вставить("Разрядность", 						"x86_64_prt");
		КонецЕсли;
			
		ПараметрыЗапуска.Вставить("Наименование",			Выборка.Наименование);
		ПараметрыЗапуска.Вставить("Кластер",				Выборка.Сервер1СНаименование + ":" + Формат(Выборка.Сервер1СПортКластера, "ЧГ="));
		ПараметрыЗапуска.Вставить("ИмяБазы",				Выборка.ИмяИБ);
		ПараметрыЗапуска.Вставить("ИдентификаторБазы",	Выборка.ИдентификаторБазы);
		ПараметрыЗапуска.Вставить("КодВЛисте",				Формат(ПозицияБазыВСписке + СчетчикЭлементов, "ЧРГ=; ЧГ=;"));
		ПараметрыЗапуска.Вставить("КодВДереве",			Формат(ПозицияБазыВСписке + СчетчикЭлементов, "ЧРГ=; ЧГ=;"));
		ПараметрыЗапуска.Вставить("ИмяГруппыБаз",			ИмяГруппыБаз);
		ПараметрыЗапуска.Вставить("ПозицияБазыВСписке",	ПозицияБазыВСписке);
		ПараметрыЗапуска.Вставить("ВерсияПлатформы",		ВерсияПлатформы);
		
		ТекстЯрлыка = ТекстЗаполненногоШаблона(ШаблонТекстаЯрлыка, ПараметрыЗапуска);
		
		МассивТекстовЯрлыков.Добавить(ТекстЯрлыка);
		
		СчетчикЭлементов = СчетчикЭлементов + 1;
	КонецЦикла;
	
	ТекстФайла = СтрСоединить(МассивТекстовЯрлыков, Символы.ПС);
	
	Возврат ТекстФайла;

КонецФункции

&НаСервере
Функция ТекстЗаполненногоШаблона(Знач ТекстШаблона, Параметры)
	
	Для Каждого ПараметрЗапуска Из Параметры Цикл
		ТекстШаблона = СтрЗаменить(ТекстШаблона, "%" + ПараметрЗапуска.Ключ + "%", ПараметрЗапуска.Значение);	
	КонецЦикла;
	
	Возврат ТекстШаблона;
	
КонецФункции
